:import [Library]
:import Factory/[CraftMagic]

;;; ---------------------------------------------------------------------------
;;; Declarations
:global int _1 ; tier
; :local int tier

;;; ---------------------------------------------------------------------------
;;; Impulse
key.3()

;;; ---------------------------------------------------------------------------
;;; Conditions
isopen("factory")

;;; ---------------------------------------------------------------------------
;;; Macros
#targetChipCount 240.

;;; ---------------------------------------------------------------------------
;;; Actions

    {craftMagic.7( \
        "chip", 2, \
        {itemLimitMax("chip", 2, {targetChipCount}, 1.)}, \
        {itemLimitMin("chip", 1, 1000., 4.)}, \
        {itemWithRatio("plate.circuit", 4, 2.)}, \
        {itemWithRatio("plate.circuit", 3, 4.)}, \
        {itemWithRatio("circuit", 4, 4.)}, \
        {itemWithRatio("circuit", 3, 2.)}, \
        {itemWithRatio("chip", 1, 4.)} \
    )}

    gotoif(SKIP_REFINERY, active("refinery"))

    ; Make circuit plate T4
    {produceMagic.2( \
        "plate", 4, "refinery", \
        {itemLimitMax.2( \
            {itemWithRatio("chip", 2, 1.)}, \
            {itemWithRatio("plate.circuit", 4, 2.)}, \
            {targetChipCount}, 0.5 \
        )}, \
        {itemLimitMin("plate", 4, 50., 1.)} \
    )}

    ; Make circuit plate T3
    {produceMagic.2( \
        "plate", 3, "refinery", \
        {itemLimitMax.2( \
            {itemWithRatio("chip", 2, 1.)}, \
            {itemWithRatio("plate.circuit", 3, 4.)}, \
            {targetChipCount}, 0.25 \
        )}, \
        {itemLimitMin("plate", 3, 50., 1.)} \
    )}

    ; Make cable T4
    {produceMagic.2( \
        "ingot", 4, "refinery", \
        {itemLimitMax("cable", 4, 50., 2.)}, \
        {itemLimitMin("ingot", 4, 100., 1.)} \
    )}

    ; Make cable T3
    {produceMagic.2( \
        "ingot", 3, "refinery", \
        {itemLimitMax("cable", 3, 50., 2.)}, \
        {itemLimitMin("ingot", 3, 100., 1.)} \
    )}

SKIP_REFINERY:

    gotoif(SKIP_ASSEMBLER, active("assembler"))

    ; Make circuit wire T4
    {produceMagic.2( \
        "cable", 4, "assembler", \
        {itemLimitMax.2( \
            {itemWithRatio("chip", 2, 1.)}, \
            {itemWithRatio("circuit", 4, 4.)}, \
            {targetChipCount}, 0.25 \
        )}, \
        {itemLimitMin("cable", 4, 10., 1.)} \
    )}

    ; Make circuit wire T3
    {produceMagic.2( \
        "cable", 3, "assembler", \
        {itemLimitMax.2( \
            {itemWithRatio("chip", 2, 1.)}, \
            {itemWithRatio("circuit", 3, 2.)}, \
            {targetChipCount}, 0.5 \
        )}, \
        {itemLimitMin("cable", 3, 10., 1.)} \
    )}

SKIP_ASSEMBLER:

