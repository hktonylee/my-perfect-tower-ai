:import [Library]
:import Factory/[CraftMagic]

;;; ---------------------------------------------------------------------------
;;; Declarations
:global int _1 ; tier
; :local int tier

;;; ---------------------------------------------------------------------------
;;; Impulse

;;; ---------------------------------------------------------------------------
;;; Conditions
isopen("factory")

;;; ---------------------------------------------------------------------------
;;; Macros
#maxPlateCount(tier) ( \
    {switch.2(double, 200., \
        ({tier}) <= 3, 400. * i2d(4 ^ (4 - ({tier}))), \
        ({tier}) <= 6, 400. \
    )} \
)
#maxIngotCount(tier) ( \
    {switch.1(double, 200., \
        ({tier}) <= 3, 200. * i2d(4 ^ (4 - ({tier}))) \
    )} \
)
#minDustCount 1000.
#maxDensePlateCount(tier) max(50., 120. - 15. * i2d({tier}))

#tier _1

;;; ---------------------------------------------------------------------------
;;; Actions

    gotoif(SKIP_OVEN, active("oven"))
    
    ; make ingot
    {produceMagic.2( \
        "dust", {tier}, "oven", \
        {itemLimitMax("ingot", {tier}, {maxIngotCount({tier})}, 1.)}, \
        {itemLimitMin("dust", {tier}, 500., 1.)} \
    )}

SKIP_OVEN:

    gotoif(SKIP_PRESSER, active("presser"))

    ; Make plate
    {produceMagic.2( \
        "ingot", {tier}, "presser", \
        {itemLimitMax("plate", {tier}, {maxPlateCount({tier})}, 1.)}, \
        {itemLimitMin("ingot", {tier}, 100., 1.)} \
    )}

    gotoif(SKIP_PRESSER, active("presser"))

    ; Make stacked plate
    {craftMagic.3( \
        "plate.stack", {tier}, \
        {itemLimitMax("plate.stack", {tier}, {maxDensePlateCount({tier})}, 1.)}, \
        {itemLimitMax("plate.dense", {tier}, {maxDensePlateCount({tier})}, 1.)}, \
        {itemLimitMin("plate", {tier}, 100., 9.)} \
    )}

    ; Make dense plate
    produce("plate.stack", {tier}, count("plate.stack", {tier}), "presser")

SKIP_PRESSER:
