:import [Library]
:import Factory/[CraftMagic]

;;; ---------------------------------------------------------------------------
;;; Declarations
:global int _1
:local int fromDustTier

;;; ---------------------------------------------------------------------------
;;; Conditions
isopen("factory")

;;; ---------------------------------------------------------------------------
;;; Macros
#minLowerTierCount(tier) ( \
    {switch.2(double, 1000., \
        ({tier}) == 1, 9000., \
        ({tier}) == 2, 3000. \
    )} \
)
#minHigherTierCount(tier) ( \
    {switch.2(double, 200., \
        ({tier}) == 1, 2000., \
        ({tier}) == 2, 1000. \
    )} \
)

#minIngotCount 3000.0

#maxMixerCount 20000.0

; Tier1: 1440
; Tier2: 500
; Tier3: 290
; Tier7+: 0 ; Want to keep 1000 of the orc first
#maxCrusherCount(tier) ( \
    {switch.1(double, 0., \
        ({tier}) < 7, (600000. / i2d(2 ^ ({tier}))) \
    )} \
)


;;; ---------------------------------------------------------------------------
;;; Actions
    fromDustTier = _1

    ; Make dust from lump
    {craftMagic.2( \
        "lump", fromDustTier, \
        {itemLimitMin("dust", fromDustTier, {minLowerTierCount(fromDustTier)}, 4.)}, \
        {itemLimitMin("dust", fromDustTier + 1, {minHigherTierCount(fromDustTier)}, 1.)} \
    )}

    ; If mixer is working, don't add more to it
    gotoif(SKIP_MIXER, active("mixer"))

    ; Make lump, so as to mix the lump into higher tier dust
    produce("lump", fromDustTier, \
        double.min({maxMixerCount}, count("lump", fromDustTier)), \
        "mixer" \
    )

SKIP_MIXER:

    ; If crusher is working, don't add more to it
    gotoif(SKIP_CRUSHER, active("crusher"))

    ; Make dust from orc
    produce("ore", fromDustTier, \
        double.min({maxCrusherCount(fromDustTier)}, count("ore", fromDustTier)), \
        "crusher" \
    )

    ; Make dust from ingot
    {produceMagic.2( \
        "ingot", fromDustTier, "crusher", \
        {minIngotCount}, \
        {itemLimitMin("ingot", fromDustTier, 3000., 1.)} \
    )}

SKIP_CRUSHER:
